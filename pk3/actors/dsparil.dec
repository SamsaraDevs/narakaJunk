actor DsparilClass : Boolean { }
actor DsparilWalking : Boolean { }
actor DsparilRevived : Boolean { }
actor ChaosDeviceCooldown : Counter { Inventory.MaxAmount 20 }
actor GhostsphereCooldown : Counter { Inventory.MaxAmount 90 }
actor DSparilSummonCooldown : Counter { Inventory.MaxAmount 10 }
actor DSparilDismountedSerpent : Boolean { }

ACTOR DsparilBeef : PowerProtection
{
  Powerup.Duration 0x7FFFFFFF
  DamageFactor "Normal", 0.875
  DamageFactor "Telefrag", 1.5
}

ACTOR DSparilSerpentArmor : BasicArmorPickup
{
	armor.saveamount 1
	armor.savepercent 50
}

actor DsparilDismount : CustomInventory
{
    inventory.usesound "heretic/itemuse"
    +INVBAR
    +inventory.undroppable
    +inventory.hubpower
    Inventory.Icon "DSPDSMNT"
	Inventory.MaxAmount 1
    States
    {
      Use:
		TNT1 A 0 A_GiveInventory("DSparilDismountedSerpent",1)
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"TLMSSummon")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"TLMSSummon")
		TNT1 A 0 A_JumpIfInventory("TLMSModeOn",1,"TLMSSummon")
		TNT1 A 0 ACS_ExecuteAlways(888, 0, 0, 0, 0)
		/*TNT1 A 0 A_GiveInventory("DSparilDismountedSerpent",1)
        TNT1 A 0 ACS_ExecuteAlways(466, 0, 1)
		TNT1 A 0 A_GiveInventory("DSparilDismountedSerpent",1)
        TNT1 A 1 A_PlaySound("dsparilserpent/active")
        TNT1 A 0 A_SpawnItemEx("DSparilSerpentUnmounted",0,0,0,0,0,0,0,49)
        TNT1 A 0 ACS_ExecuteAlways(465)
        TNT1 A -1*/
        fail
		
	  TLMSSummon:
		TNT1 A 0 ACS_ExecuteAlways(889, 0, 0, 0, 0)
		/*TNT1 A 0 A_GiveInventory("DSparilDismountedSerpent",1)
        TNT1 A 0 ACS_ExecuteAlways(466, 0, 1)
		TNT1 A 0 A_GiveInventory("DSparilDismountedSerpent",1)
        TNT1 A 1 A_PlaySound("dsparilserpent/active")
        TNT1 A 0 A_SpawnItemEx("DSparilSerpentUnmountedTLMS",0,0,0,0,0,0,0,49)
        TNT1 A 0 ACS_ExecuteAlways(465)
        TNT1 A -1*/
        fail
    }
}

actor DsparilMount : CustomInventory
{
    inventory.usesound "heretic/itemuse"
    +INVBAR
    +inventory.undroppable
    +inventory.hubpower
    Inventory.Icon "DSPMOUNT"
	Inventory.MaxAmount 1
    States
    {
      Use:
		TNT1 A 0 ACS_ExecuteAlways(889, 0, 1, 0, 0)
        fail
    }
}

actor DSparilSerpentUnmounted : Demon1B
{
    Health 750
	Mass 600
    DamageFactor "DSparilDamage", 0.0
    DamageFactor "DukeExplosive", 0.35
    +THRUSPECIES
    Species "Villain"
	+NOBLOCKMONST
	+DONTRIP
	+NORADIUSDMG
	+MISSILEMORE
	+MISSILEEVENMORE
	+FRIENDLY
	//+FLOORHUGGER
	MinMissileChance 10
	Speed 20
	Scale 0.75
	SeeSound "dsparilserpent/sight"
	AttackSound "dsparilserpent/attack"
	PainSound "dsparilserpent/Pain"
	DeathSound "dsparilserpent/Death"
	ActiveSound "dsparilserpent/Active"
	Obituary "%k \crfed\c- %o \crto the Serpent.\c-"
	States
	{
	  Spawn:
		TNT1 A 0 A_GiveInventory("IsNarakaClass",1)
		DEMN AA 10 A_Look
		goto See
		
      See:
		TNT1 A 0 A_GiveInventory("IsNarakaClass",1)
        TNT1 A 0 A_SetTranslucent(1,0)
        DEMN A 2 A_Chase
		TNT1 A 0 A_PlaySoundEx("dsparilserpent/step","soundslot7",0,0)
		DEMN A 3 A_Chase
		DEMN B 2 A_Chase
		DEMN B 3 A_Chase
		DEMN C 2 A_Chase
		TNT1 A 0 A_PlaySoundEx("dsparilserpent/step","soundslot7",0,0)
		DEMN C 3 A_Chase
		DEMN D 2 A_Chase
		DEMN D 3 A_Chase
        Loop
		
	  Melee:
		TNT1 A 0 A_JumpIfInTargetInventory("DsparilClass",1,"OhBuggerNotHim")
		DEMN EF 3 A_FaceTarget
		TNT1 A 0 A_PlaySound("dsparilserpent/bite")
		DEMN G 4 A_CustomMeleeAttack(random(1,8)*8)
		Goto See
		
	  Missile:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_JumpIfInTargetInventory("DsparilClass",1,"OhBuggerNotHim")
		TNT1 A 0 A_Jump(32,"Missile2")
		DEMN EF 4 A_FastChase
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_PlaySound("dsparilserpent/attack")
		DEMN G 4 A_CustomMissile("DSparilFireball", 62, 0)
		DEMN FE 2 A_FastChase
		TNT1 A 0 A_Jump(128,"Missile")
		Goto See
		
	  Missile2:
		DEMN EF 5 A_FaceTarget
		TNT1 A 0 A_PlaySound("dsparilserpent/attack2")
		TNT1 A 0 A_CustomMissile("DSparilFireballSerpent2", 62, 0, -3)
		TNT1 A 0 A_CustomMissile("DSparilFireballSerpent2", 62, 0, 3)
		DEMN G 5 A_CustomMissile("DSparilFireballSerpent2", 62, 0)
		DEMN FE 3
		DEMN A 2 A_FastChase
		DEMN EF 5 A_FaceTarget
		TNT1 A 0 A_PlaySound("dsparilserpent/attack2")
		TNT1 A 0 A_CustomMissile("DSparilFireballSerpent2", 62, 0, -3)
		TNT1 A 0 A_CustomMissile("DSparilFireballSerpent2", 62, 0, 3)
		DEMN G 5 A_CustomMissile("DSparilFireballSerpent2", 62, 0)
		DEMN FE 3
		DEMN A 2 A_FastChase
		TNT1 A 0 A_Jump(64,"Missile")
		Goto See
		
	  OhBuggerNotHim:
        TNT1 A 0 A_ClearTarget
        DEMN A 2 A_Wander
		TNT1 A 0 A_PlaySoundEx("dsparilserpent/step","soundslot7",0,0)
		DEMN A 3 A_Wander
		DEMN B 2 A_Wander
		DEMN B 3 A_Wander
		DEMN C 2 A_Wander
		TNT1 A 0 A_PlaySoundEx("dsparilserpent/step","soundslot7",0,0)
		DEMN C 3 A_Wander
		DEMN D 2 A_Wander
		DEMN D 3 A_Wander
        Loop

      Death:
		TNT1 A 0 Thing_ChangeTID(0, 0)
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        goto "Super::Death"

      XDeath:
		TNT1 A 0 Thing_ChangeTID(0, 0)
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        goto "Super::XDeath"

      Death.ZorchPistol:

      Death.Zorch:
		TNT1 A 0 Thing_ChangeTID(0, 0)
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_Stop
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        DEMN E 4 A_SpawnItem("ZorchEffectBig")
        DEMN E 4 A_SetTranslucent(0.9,0)
        TNT1 A 0 A_Scream
        DEMN E 4 A_SetTranslucent(0.8,0)
        TNT1 A 0 A_NoBlocking
        DEMN E 4 A_SetTranslucent(0.7,0)
        DEMN E 4 A_SetTranslucent(0.6,0)
        DEMN E 4 A_SetTranslucent(0.5,0)
        DEMN E 4 A_SetTranslucent(0.4,0)
        DEMN E 4 A_SetTranslucent(0.3,0)
        DEMN E 4 A_SetTranslucent(0.2,0)
        DEMN E 4 A_SetTranslucent(0.1,0)
        TNT1 A 1 A_SetTranslucent(1,0)
        TNT1 A -1 A_QueueCorpse
        stop

      Ice:
      Death.HexenIce:

      Death.Ice:
		TNT1 A 0 Thing_ChangeTID(0, 0)
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        DEMN Q 0 A_UnsetSolid
        DEMN Q 70 A_GenericFreezeDeath
        DEMN Q 0 A_IceGuyDie
        DEMN Q 1 A_IceGuyDie
        TNT1 A -1
        stop

      Death.DukeDamage:

      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      Death.DukeExplosive:
        TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
        goto Death.DukeDamage

      DukeGibStart:
        DEMN E 1
        TNT1 A 0 A_UnsetSolid
        TNT1 A 0 A_SpawnDebris("DukeGibs1")
        TNT1 A 0 A_SpawnDebris("DukeGibs2")
        TNT1 A 0 A_SpawnDebris("DukeGibs3")
        TNT1 A 0 A_SpawnDebris("DukeGibs4")
        TNT1 A 0 A_SpawnDebris("DukeGibs5")
        TNT1 A 0 A_SpawnDebris("DukeGibs6")
        TNT1 A 0 A_PlaySound("duke/enemygib")
        TNT1 A 0 A_Jump(96,"XDeath")
        TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
        goto XDeath

      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Ice
	}
}

actor DSparilSerpentUnmountedTLMS : DSparilSerpentUnmounted
{
	DamageFactor "VillainTeam", 0.0
	States
	{
	  Melee:
		TNT1 A 0 A_JumpIfInTargetInventory("IsNarakaClass",1,"OhBuggerNotHim")
		DEMN EF 3 A_FaceTarget
		TNT1 A 0 A_PlaySound("dsparilserpent/bite")
		DEMN G 4 A_CustomMeleeAttack(random(1,8)*8)
		Goto See
		
	  Missile:
		TNT1 A 0 A_JumpIfInTargetInventory("IsNarakaClass",1,"OhBuggerNotHim")
		TNT1 A 0 A_Jump(32,"Missile2")
		DEMN EF 4 A_FaceTarget
		DEMN G 4 A_CustomMissile("DSparilFireballSerpentTLMS", 62, 0)
		DEMN FE 2 A_FastChase
		TNT1 A 0 A_Jump(128,"Missile")
		Goto See
		
	  Missile2:
		DEMN EF 5 A_FaceTarget
		TNT1 A 0 A_CustomMissile("DSparilFireballSerpent2TLMS", 62, 0, -3)
		TNT1 A 0 A_CustomMissile("DSparilFireballSerpent2TLMS", 62, 0, 3)
		DEMN G 5 A_CustomMissile("DSparilFireballSerpent2TLMS", 62, 0)
		DEMN FE 3
		DEMN A 2 A_FastChase
		DEMN EF 5 A_FaceTarget
		TNT1 A 0 A_CustomMissile("DSparilFireballSerpent2TLMS", 62, 0, -3)
		TNT1 A 0 A_CustomMissile("DSparilFireballSerpent2TLMS", 62, 0, 3)
		DEMN G 5 A_CustomMissile("DSparilFireballSerpent2TLMS", 62, 0)
		DEMN FE 3
		DEMN A 2 A_FastChase
		TNT1 A 0 A_Jump(64,"Missile")
		Goto See
	}
}

actor DsparilPortTeleport : CustomInventory
{
    //tag "Chaos Device"
    inventory.maxamount 1
    inventory.usesound "heretic/itemuse"
    +INVBAR
    +inventory.undroppable
    +inventory.hubpower
    Inventory.Icon "ARTIATLP"
    States
    {
      Use:
        TNT1 A 0 A_JumpIfInventory("ChaosDeviceCooldown",1,"UseFail")
        TNT1 A 1
        TNT1 A 0 A_SpawnItemEx("DsparilTeleport",0,0,0,0,0,0,0,33)
        TNT1 A 0 A_GiveInventory("DSparilChaosDevice",1)
        TNT1 A 0 A_GiveInventory("ChaosDeviceCooldown",20)
        TNT1 A 0 ACS_ExecuteAlways(484, 0, 4, 35)
        fail
        
      UseFail:
        TNT1 A 1
        TNT1 A 0 A_Print("The Chaos Device has not cooled down yet.")
        fail
    }
}

actor DsparilPortGhostsphere : CustomInventory
{
    //tag "Shadowsphere"
    +INVBAR
    +INVENTORY.UNDROPPABLE
    Inventory.Icon "ARTINVSI"
    +inventory.hubpower
    inventory.usesound "heretic/itemuse"
    Inventory.MaxAmount 1
    States
    {
      Use:
        TNT1 A 0 A_JumpIfInventory("GhostsphereCooldown",1,"UseFail")
        TNT1 A 1
        TNT1 A 0 A_GiveInventory("PowerGhost",1)
        TNT1 A 0 A_GiveInventory("GhostsphereCooldown",90)
        TNT1 A 0 ACS_ExecuteAlways(484, 0, 0, 35)
        fail
        
      UseFail:
        TNT1 A 1
        TNT1 A 0 A_Print("The Shadowsphere has not cooled down yet.")
        fail
    }
}

actor DSparilChaosDevice : ArtiTeleport
{
    +INVENTORY.AUTOACTIVATE
}

actor DsparilPlayer : PlayerPawn
{
	Health 150
	Player.MaxHealth 150
    LimitedToTeam 1
    Player.DisplayName "DSparil"
    BloodColor "Red"
    Player.Face "EVL"
    Radius 16 // Previously 25
    Height 59
    Mass 800
    Player.Viewheight 65
    Player.AttackZOffset 22
    //Player.ForwardMove 0.95 0.75
    //Player.SideMove 0.85 0.85
    Scale 0.75
    Player.JumpZ 8//Default is 8
    Player.StartItem "Chaos Serpent", 1
    Player.StartItem "DsparilClass", 1
	Player.StartItem "IsNarakaClass", 1
	Player.StartItem "VillainAmmoLight", 50
	Player.StartItem "DsparilDismount", 1
    //Player.StartItem "BossDamageImmunity", 1
    Player.Soundclass "DSparil"
    DamageFactor "VillainTeam", 0
    DamageFactor "DSparilDamage", 0
    DamageFactor "Slime", 0
    DamageFactor "Lava", 0
    DamageFactor "LavaHefty", 0
    DamageFactor "Nuke", 0
    DamageFactor "Nukage", 0
    DamageFactor "Hellslime", 0
    DamageFactor "SuperHellslime", 0
    DamageFactor "Telefrag", 2.0
    DamageFactor "PoisonCloud", 1.75
    DamageFactor "DukeExplosive", 0.75
    DamageFactor "TimonsAxe", 0.55
    DamageFactor "WMCBeam", 0.55
    DamageFactor "WMCNade", 0.55
    Species "Villain"
    -THRUSPECIES
    +NOSKIN
    -DONTBLAST
    +DONTRIP
    +BOSSDEATH
    +NOTDMATCH
    +NOICEDEATH
    +NORADIUSDMG
    //Species "Player"
    Player.ColorRange 176, 191
    States
    {
      CoOpModeOn:
        TNT1 A 0
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",1)
        SRCR A 1
        goto Spawn
		
      Spawn:
        TNT1 A 0 A_TakeInventory("DSparilDismountedSerpent",100)
        TNT1 A 0 A_TakeInventory("DSparilWalking",100)
        TNT1 A 0 A_TakeInventory("DSparilRevived",100)
        TNT1 A 0 A_TakeInventory("DSparilPortTeleport",100)
        TNT1 A 0 A_TakeInventory("DSparilPortGhostsphere",100)
        TNT1 A 0 A_TakeInventory("Staff of D'Sparil",100)
        TNT1 A 0 A_SetShootable
        TNT1 A 0 A_JumpIfHealthLower(50,"FakeDeath")
        SRCR AAAAA 1
        Loop
        
      See:
        TNT1 A 0 A_TakeInventory("DSparilDismountedSerpent",100)
        TNT1 A 0 A_GiveInventory("DSparilWalking",1)
        SRCR A 2
        SRCR A 0 A_CheckFloor("WalkSound")
        SRCR A 2
        goto ContinueOn

      WalkSound:
        //TNT1 A 0 A_AlertMonsters
        SRCR A 2 A_PlaySound("dsparilserpent/step")

      ContinueOn:
        SRCR ABB 2
        SRCR C 2
        SRCR A 0 A_CheckFloor("WalkSound2")
        SRCR C 2
        goto ContinueOn2

      WalkSound2:
        //TNT1 A 0 A_AlertMonsters
        SRCR C 2 A_PlaySound("dsparilserpent/step")

      ContinueOn2:
        TNT1 A 0 A_Jump(16,"SerpentShouts")
        SRCR CDD 2
        goto Spawn
        
      SerpentShouts:
        TNT1 A 0 A_PlaySoundEx("dsparilserpent/sight", "soundslot6")
        SRCR CDD 2
        goto Spawn
        
      Missile:
        SRCR Q 12 
        Goto Spawn
        
      Melee:
        SRCR S 6 BRIGHT
        Goto Missile
        
      Pain:
        TNT1 A 0 A_JumpIfHealthLower(50,"FakeDeath")
        SRCR R 4 
        SRCR R 4 A_Pain
        Goto Spawn
        
      Pain.Zorch:
      Pain.ZorchPistol:
        TNT1 A 0 A_JumpIfHealthLower(50,"FakeZorch")
        SRCR R 4 
        SRCR R 4 A_Pain
        Goto Spawn
        
      Pain.Ice:
      Pain.DukeIce:
      Pain.HexenIce:
        TNT1 A 0 A_JumpIfHealthLower(50,"FakeFreeze")
        SRCR R 4 
        SRCR R 4 A_Pain
        Goto Spawn
        
      Death.DukeDamage:
      Death.DukeExplosive:
      Death.DukePistol:
      Death.MightyBoot:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBossKillTaunt",1)
        goto Death
        
      XDeath:
      Death:
        SRCR E 1 A_SpawnItemEx("DSparilSerpentDeath",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_PlayerScream
        TNT1 A 0 A_NoBlocking
        ACLO E 1
        ACLO E -1
        stop
        
      /*Death.DukeIce:
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeBossKillTaunt",1)
        goto Ice
        
      Death.ZorchPistol:
      Death.Zorch:
        SRCR E 0 A_Stop
        SRCR E 1 A_SpawnItemEx("DSparilSerpentZorch",0,0,0,0,0,0,0,31)
        TNT1 A 0 A_NoBlocking
        ACLO E 1
        ACLO E -1
        stop
        
      Ice:
      Death.HexenIce:
      Death.Ice:
        SRCR E 1 A_SpawnItemEx("DSparilSerpentFreeze",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_NoBlocking
        ACLO E 1
        ACLO E -1
        stop*/
        
        
        
      FakeDeath:
        TNT1 A 0 A_UnsetShootable//A_ChangeFlag("Shootable",0)
        SRCR E 1 A_TakeInventory("DSparilDismount")
        SOR2 A 0 ACS_ExecuteAlways(466, 0, 1)
        SRCR E 7
        SRCR F 7 A_PlaySound("dsparilserpent/death")
        SRCR G 7
        SRCR HIJK 6
        SRCR L 1 A_SpawnItemEx("DSparilSerpentDeath",0,0,0,0,0,0,0,129)
        SRCR L 6 ACS_ExecuteAlways(465)
        TNT1 A -1
        stop
        
      FakeZorch:
        TNT1 A 0 A_UnsetShootable//A_ChangeFlag("Shootable",0)
        SRCR Q 1 A_TakeInventory("DSparilDismount")
        SOR2 A 0 ACS_ExecuteAlways(466, 0, 1)
        TNT1 A 0 A_PlaySoundEx("chex/zorchhuge","soundslot5",0,0)
        SRCR Q 1
        SRCR Q 8 A_SpawnItemEx("ZorchEffectHuge",0,0,0,0,0,0,0,129)
        SRCR Q 8 A_SetTranslucent(0.9,0)
        SRCR Q 0 A_PlaySoundEx("dsparilserpent/active","soundslot6",0,2)
        SRCR Q 8 A_SetTranslucent(0.8,0)
        SRCR Q 0 A_NoBlocking
        SRCR Q 8 A_SetTranslucent(0.7,0)
        SRCR Q 8 A_SetTranslucent(0.6,0)
        SRCR Q 8 A_SetTranslucent(0.5,0)
        SRCR Q 8 A_SetTranslucent(0.4,0)
        SRCR Q 8 A_SetTranslucent(0.3,0)
        SRCR Q 8 A_SetTranslucent(0.2,0)
        SRCR Q 8 A_SetTranslucent(0.1,0)
        ACLO E 1 A_SetTranslucent(1,0)
        ACLO E 6 ACS_ExecuteAlways(465)
        TNT1 A -1
        stop
        
      FakeFreeze:
        TNT1 A 0 A_UnsetShootable//A_ChangeFlag("Shootable",0)
        SRCR Z 1 A_TakeInventory("DSparilDismount")
        SOR2 A 0 ACS_ExecuteAlways(466, 0, 1)
        SRCR Q 0 A_PlaySoundEx("freeze/dsparil","soundslot6",0,0)
        //SRCR Q 0 A_UnsetShootable
        SRCR Z 140 //A_UnsetSolid
        SRCR Q 0 A_PlaySoundEx("freeze/break","soundslot6",0,0)
        SRCR GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 0 A_SpawnItemEx("IceChunk2",random(-1,1),random(-1,1),random(16,96),random(-2,2),random(-2,2),random(5,15))
        ACLO E 6 ACS_ExecuteAlways(465)
        TNT1 A -1
        stop
    }
}

actor DsparilOnFootPlayer : PlayerPawn
{
	Health 150
	Player.MaxHealth 150
    LimitedToTeam 1
    Player.DisplayName "DSparil"
    BloodColor "Red"
    //Player.Face "EVL"
    Radius 16
    Height 45
    Mass 300
    Player.Viewheight 45
    Player.AttackZOffset 22
    Player.ForwardMove 0.75 0.7
    Player.SideMove 0.6 0.6
    Scale 0.75
    Player.JumpZ 6//Default is 8
    Player.StartItem "Staff of D'Sparil", 1
    Player.StartItem "DsparilClass", 1
	Player.StartItem "IsSamsaraClass", 1
    //Player.StartItem "BossDamageImmunity", 1
    Player.StartItem "DsparilPortTeleport", 1
    Player.StartItem "DsparilPortGhostsphere", 1
    Player.Soundclass "DSparilOnFoot"
	RadiusDamageFactor 0.5
    DamageFactor "VillainTeam", 0
    DamageFactor "DSparilDamage", 0
    DamageFactor "Slime", 0
    DamageFactor "Lava", 0
    DamageFactor "Nuke", 0
    DamageFactor "Telefrag", 2.0
    DamageFactor "PoisonCloud", 1.75
    DamageFactor "DukeExplosive", 0.75
    DamageFactor "TimonsAxe", 0.55
    DamageFactor "WMCBeam", 0.55
    DamageFactor "WMCNade", 0.55
    Player.MorphWeapon "Staff of D'Sparil"
    Species "Villain"
    //DamageFactor "CorvusHellstaffTomed", 0.2
    //+NOTARGET - Doesn't work, surprisingly.
    +THRUSPECIES
    +NOSKIN
    -DONTBLAST
    +DONTRIP
    +BOSSDEATH
    +NOTDMATCH
    +NOICEDEATH
    //+NORADIUSDMG
    +INVULNERABLE
    -SHOOTABLE
    //Species "Player"
    Player.ColorRange 176, 191
    States
    {
      Spawn:
		TNT1 A 0 A_JumpIfInventory("DSparilRevived",1,"Spawn2")
		TNT1 A 0 A_JumpIfInventory("DSparilDismountedSerpent",1,"Spawn3")
        ACLO E 1 A_JumpIfInventory("DSparilRevived",1,"Spawn2")
		ACLO E 1 A_JumpIfInventory("DSparilDismountedSerpent",1,"Spawn3")
		ACLO E 1 A_JumpIfInventory("DSparilRevived",1,"Spawn2")
		ACLO E 1 A_JumpIfInventory("DSparilDismountedSerpent",1,"Spawn3")
		ACLO E 1 A_JumpIfInventory("DSparilRevived",1,"Spawn2")
		ACLO E 1 A_JumpIfInventory("DSparilDismountedSerpent",1,"Spawn3")
		ACLO E 1 A_JumpIfInventory("DSparilRevived",1,"Spawn2")
		ACLO E 1 A_JumpIfInventory("DSparilDismountedSerpent",1,"Spawn3")
		ACLO E 1 A_JumpIfInventory("DSparilRevived",1,"Spawn2")
		ACLO E 1 A_JumpIfInventory("DSparilDismountedSerpent",1,"Spawn3")
		// The shit I do to accomodate for Morphclass.
        SOR2 A 0 ACS_ExecuteAlways(466, 0, 1)
        SOR2 A 0 ACS_ExecuteAlways(473)
        ACLO E 70 A_TakeInventory("DSparilDismount")
        ACLO E 0 ACS_ExecuteAlways(475)//A_GiveInventory("Megasphere")
        ACLO E 0 A_GiveInventory("DsparilPortTeleport")
        ACLO E 0 A_GiveInventory("DsparilPortGhostsphere")
        SOR2 AB 4 A_TakeInventory("DsparilBeef")
        SOR2 C 4 A_PlaySoundEx("dsparil/rise", "Body")
        SOR2 DEF 4 A_GiveInventory("DSparilRevived")
        SOR2 G 12 A_PlaySoundEx("dsparil/sight", "Body")
        SOR2 A 0 A_ChangeFlag("Invulnerable",0)
        SOR2 A 0 A_ChangeFlag("Shootable",1)
        SOR2 A 0 ACS_ExecuteAlways(466, 0, 0)
        Goto Spawn2

      Spawn2:
        TNT1 A 0 A_ChangeFlag("Invulnerable",0)
        TNT1 A 0 A_ChangeFlag("Shootable",1)
		TNT1 A 0 A_GiveInventory("DSparilRevived")
        SOR2 M -1
        Loop
		
	  Spawn3:
        SOR2 A 0 ACS_ExecuteAlways(466, 0, 1)
        SOR2 A 0 ACS_ExecuteAlways(473)
        SOR2 G 1 A_TakeInventory("DSparilDismountedSerpent")
        SOR2 G 1 A_TakeInventory("DSparilDismount")
        ACLO E 0 //ACS_ExecuteAlways(475)//A_GiveInventory("Megasphere")
        ACLO E 0 A_GiveInventory("DsparilPortTeleport")
        ACLO E 0 A_GiveInventory("DsparilPortGhostsphere")
        SOR2 G 1 A_TakeInventory("DsparilBeef")
        SOR2 G 1 A_GiveInventory("DSparilRevived")
        SOR2 G 4 A_PlaySoundEx("dsparil/sight", "Body")
        SOR2 A 0 A_ChangeFlag("Invulnerable",0)
        SOR2 A 0 A_ChangeFlag("Shootable",1)
        SOR2 A 0 ACS_ExecuteAlways(466, 0, 0)
        Goto Spawn2
        
      See:
        SOR2 MNOP 4
        goto Spawn2
        
      Missile:
      Melee:
        SOR2 RST 6 
        Goto Spawn2
        
        //Melee:
        //SOR2 T 6 BRIGHT
        //Goto Missile
        
      Pain:
        SOR2 Q 4 
        SOR2 Q 4 A_Pain
        Goto Spawn2
        
      Pain.Zorch:
      Pain.ZorchPistol:
        SOR2 Q 4 
        SOR2 Q 4 A_Pain
        Goto Spawn
        
      Pain.Ice:
      Pain.DukeIce:
      Pain.HexenIce:
        SOR2 Q 4 
        SOR2 Q 4 A_Pain
        Goto Spawn
        
      Death.DukeDamage:
      Death.DukeExplosive:
      Death.DukePistol:
      Death.MightyBoot:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBossKillTaunt",1)
        goto Death
        
      XDeath:
      Death:
        SDTH A 1 A_KillChildren
        SDTH A 1 A_SpawnItemEx("DSparilDeath",0,0,0,0,0,0,0,128)
        TNT1 A 0 A_NoBlocking
        ACLO E -1
        stop
        
      Death.ZorchPistol:
      Death.Zorch:
        SOR2 Q 1 A_KillChildren
        SOR2 Q 1 A_SpawnItemEx("DSparilZorch",0,0,0,0,0,0,0,128)
        TNT1 A 0 A_NoBlocking
        ACLO E -1
        stop
        
      Death.DukeIce:
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeBossKillTaunt",1)
        goto Ice
        
      Ice:
      Death.HexenIce:
      Death.Ice:
        SOR2 Q 1 A_KillChildren
        SOR2 Q 1 A_SpawnItemEx("DSparilFrozen",0,0,0,0,0,0,0,128)
        TNT1 A 0 A_NoBlocking
        ACLO E -1
        stop
    }
}

actor DSparilDeath
{
    Radius 1
    Height 1
    +NOCLIP
    -SOLID
    +CLIENTSIDEONLY
    +NOINTERACTION
    Scale 0.85
    States
    {
      Spawn:
        SDTH A 8
        SDTH B 8
        SDTH C 8 A_PlaySoundEx("dsparil/scream", "Body")
        SDTH DEFDEFDEFDEFDEFDEFDEF 7
        SDTH G 6 A_PlaySoundEx("dsparil/explode", "Body")
        SDTH H 6
        SDTH I 18
        SDTH J 6 A_NoBlocking
        SDTH K 6 A_PlaySoundEx("dsparil/bones", "Body")
        SDTH LMN 6
        SDTH O -1
    }
}

actor DSparilZorch
{
    Radius 1
    Height 1
    +NOCLIP
    -SOLID
    +CLIENTSIDEONLY
    +NOINTERACTION
    Scale 0.8
    States
    {
      Spawn:
        SOR2 Q 1 A_PlaySoundEx("chex/zorchhuge","soundslot5",0,0)
        SOR2 Q 8 A_SpawnItem("ZorchEffectHuge2")
        SOR2 Q 8 A_SetTranslucent(0.9,0)
        TNT1 A 0 A_PlaySoundEx("chex/dsparilzorch","soundslot6",0,2)
        SOR2 Q 8 A_SetTranslucent(0.8,0)
        TNT1 A 0 A_NoBlocking
        SOR2 Q 8 A_SetTranslucent(0.7,0)
        SOR2 Q 8 A_SetTranslucent(0.6,0)
        SOR2 Q 8 A_SetTranslucent(0.5,0)
        SOR2 Q 8 A_SetTranslucent(0.4,0)
        SOR2 Q 8 A_SetTranslucent(0.3,0)
        SOR2 Q 8 A_SetTranslucent(0.2,0)
        SOR2 Q 8 A_SetTranslucent(0.1,0)
        ACLO E -1
        stop
    }
}

actor DSparilFrozen
{
    Radius 1
    Height 1
    +NOCLIP
    -SOLID
    +CLIENTSIDEONLY
    +NOINTERACTION
    Scale 0.8
    States
    {
      Spawn:
        SOR2 Q 0 A_PlaySoundEx("freeze/dsparil","soundslot6",0,0)
        SOR2 Q 0 A_UnsetShootable
        SOR2 Z 105 A_UnsetSolid
        SOR2 Q 0 A_PlaySoundEx("freeze/break","soundslot6",0,0)
        SOR2 QQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQ 0 A_SpawnItemEx("IceChunk2",random(-1,1),random(-1,1),random(16,96),random(-2,2),random(-2,2),random(5,15))
        ACLO E -1
        stop
    }
}

actor DSparilSerpentDeath
{
    Radius 1
    Height 1
    +NOCLIP
    -SOLID
    +CLIENTSIDEONLY
    //+NOINTERACTION
    Scale 0.85
    States
    {
      Spawn:
        SRCR L 25
        SRCR L 0 A_PlaySoundEx("dsparil/zap", "body")
        SRCR MN 5
        SRCR O 5
        SRCR L 20
        SRCR L 0 A_PlaySoundEx("dsparil/zap", "body")
        SRCR MN 5
        SRCR O 4
        SRCR L 6
        SRCR P -1 //A_SorcererRise
    }
}

actor DSparilTeleport
{
    Radius 1
    Height 1
    Scale 0.85
    Speed 0
    +NOCLIP
    -SOLID
    +CLIENTSIDEONLY
    +NOINTERACTION
    States
    {
      Spawn:
        SOR2 G 7
        SOR2 H 7
        SOR2 I 7
        SOR2 J 7
        SOR2 K 7
        SOR2 L 7
        ACLO E 1
        ACLO E -1
        stop
    }
}

actor "Chaos Serpent" : Weapon
{
    Weapon.SlotNumber 9
    +AMMO_OPTIONAL
    +ALT_AMMO_OPTIONAL
    Weapon.Kickback 100
	Obituary "%k \crfed\c- %o \crto the Serpent."
    States
    {
      Ready:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"ReadyCoop")
		TNT1 A 0 A_JumpIfInventory("TLMSModeOn",1,"ReadyCoop")
        TNT1 A 0 A_JumpIfInventory("DSparilWalking",1,"Walking")
        SPRN A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfCloser(128,"Nomnomnom")
        Loop
		
      ReadyCoop:
        TNT1 A 0 A_JumpIfInventory("DSparilWalking",1,"WalkingCoop")
        SPRN A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfCloser(128,"NomnomnomCoopTLMS")
        Loop
		
	  Nomnomnom:
        SPRN BC 3 A_WeaponReady(1)
		TNT1 A 0 A_PlayWeaponSound("dsparilserpent/bite")
		TNT1 A 0 A_CustomPunch(random(1,8)*8,1,0,"BlankPuff",160)//A_Saw("dsparilserpent/bite","",random(1,8)*8,"EmptyPuff")//,0,160)
        SPRN D 4 A_WeaponReady(1)
        goto Ready
		
	  NomnomnomCoopTLMS:
		TNT1 A 0 A_JumpIfInTargetInventory("IsNarakaClass",1,"ReadyCoop")
        SPRN BC 3 A_WeaponReady(1)
		TNT1 A 0 A_PlayWeaponSound("dsparilserpent/bite")
		TNT1 A 0 A_CustomPunch(random(1,8)*8,1,0,"BlankPuff",160)//A_Saw("dsparilserpent/bite","",random(1,8)*8,"EmptyPuff")//,0,160)
        SPRN D 4 A_WeaponReady(1)
        goto Ready

      Walking:
		TNT1 A 0 A_JumpIfInventory("TLMSModeOn",1,"WalkingCoop")
		SPRN E 1 A_JumpIfInventory("CoopModeOn",1,"WalkingCoop")
		TNT1 A 0 A_JumpIfCloser(128,"Nomnomnom")
        SPRN EEEEE 1 A_WeaponReady
		TNT1 A 0 A_JumpIfCloser(128,"Nomnomnom")
		SPRN FF 2 A_WeaponReady
		TNT1 A 0 A_JumpIfCloser(128,"Nomnomnom")
		SPRN GGG 2 A_WeaponReady
		TNT1 A 0 A_JumpIfCloser(128,"Nomnomnom")
		SPRN AA 2 A_WeaponReady
        goto Ready

      WalkingCoop:
		SPRN E 1
		TNT1 A 0 A_JumpIfCloser(128,"NomnomnomCoopTLMS")
        SPRN EEEEE 1 A_WeaponReady
		TNT1 A 0 A_JumpIfCloser(128,"NomnomnomCoopTLMS")
		SPRN FF 2 A_WeaponReady
		TNT1 A 0 A_JumpIfCloser(128,"NomnomnomCoopTLMS")
		SPRN GGG 2 A_WeaponReady
		TNT1 A 0 A_JumpIfCloser(128,"NomnomnomCoopTLMS")
		SPRN AA 2 A_WeaponReady
        goto Ready

      Deselect:
        SPRN A 1 A_Lower
        Loop

      Select:
        SPRN A 1 A_Raise
        Loop

      Fire:
        SPRN BC 2
        SPRN A 0 A_GunFlash
        SPRN A 0 A_PlayWeaponSound("dsparilserpent/attack")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        SPRN D 4 A_FireCustomMissile("DSparilFireball",0,0,0,-3)
        SPRN B 6 
        goto Ready
		
	  CoopFire:
        SPRN D 4 A_FireCustomMissile("DSparilFireballCoop",0,0,0,-3)
        SPRN B 6 
        goto Ready

      AltFire:
        SPRN B 2
        SPRN C 3
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_PlayWeaponSound("dsparilserpent/attack2")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopAltFire")
        TNT1 A 0 A_FireCustomMissile("DSparilFireball2",-3,0,0,-3)
        TNT1 A 0 A_FireCustomMissile("DSparilFireball2",0,0,0,-3)
        TNT1 A 0  A_FireCustomMissile("DSparilFireball2",3,0,0,-3)
        SPRN D 5
        SPRN C 4
        SPRN B 3
        SPRN A 3
        SPRN B 2
        SPRN C 3
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_PlayWeaponSound("dsparilserpent/attack2")
        TNT1 A 0 A_FireCustomMissile("DSparilFireball2",-3,0,0,-3)
        TNT1 A 0 A_FireCustomMissile("DSparilFireball2",0,0,0,-3)
        TNT1 A 0 A_FireCustomMissile("DSparilFireball2",3,0,0,-3)
        SPRN D 5
        SPRN C 4
        SPRN B 3
        SPRN A 23
        goto Ready
		
	  CoopAltFire:
        TNT1 A 0 A_FireCustomMissile("DSparilFireballCoop2",-3,0,0,-3)
        TNT1 A 0 A_FireCustomMissile("DSparilFireballCoop2",0,0,0,-3)
        TNT1 A 0  A_FireCustomMissile("DSparilFireballCoop2",3,0,0,-3)
        SPRN D 5
        SPRN C 4
        SPRN B 3
        SPRN A 3
        SPRN B 2
        SPRN C 3
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_PlayWeaponSound("dsparilserpent/attack2")
        TNT1 A 0 A_FireCustomMissile("DSparilFireballCoop2",-3,0,0,-3)
        TNT1 A 0 A_FireCustomMissile("DSparilFireballCoop2",0,0,0,-3)
        TNT1 A 0 A_FireCustomMissile("DSparilFireballCoop2",3,0,0,-3)
        SPRN D 5
        SPRN C 4
        SPRN B 3
        SPRN A 23
        goto Ready
    }
}

actor DSparilFireball : SorcererFX1
{
	Damage (random(5,50))
    Obituary "%k \crfed\c- %o \crto the Serpent.\c-"
    Speed 35
    FastSpeed 35
}

actor DSparilFireball2 : SorcererFX1
{
	Damage (random(5,50))
    Obituary "%k \crfed\c- %o \crto the Serpent.\c-"
    Speed 25
    FastSpeed 25
}

actor DSparilFireballSerpent : DSparilFireball
{
	DamageType "DSparilDamage"
}

actor DSparilFireballSerpent2 : DSparilFireball
{
	DamageType "DSparilDamage"
}

actor DSparilFireballSerpentTLMS : DSparilFireball
{
	DamageType "VillainTeam"
}

actor DSparilFireballSerpent2TLMS : DSparilFireball
{
	DamageType "VillainTeam"
}

actor DSparilFireballCoop : SorcererFX1
{
	+THRUSPECIES
	Species "Villain"
    //Obituary "%k fed %o to the Serpent."
    Speed 35
    FastSpeed 35
}

actor DSparilFireballCoop2 : SorcererFX1
{
	+THRUSPECIES
	Species "Villain"
    //Obituary "%k fed %o to the Serpent."
    Speed 25
    FastSpeed 25
}

actor "Staff of D'Sparil" : weapon
{
    Weapon.SlotNumber 9
	Weapon.AmmoType "VillainAmmoLight"
	Weapon.AmmoType2 "VillainAmmoHeavy"
	Weapon.AmmoGive 0
	Weapon.AmmoGive2 0
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 1
    +AMMO_OPTIONAL
    +ALT_AMMO_OPTIONAL
    Weapon.Kickback 100
    States
    {
      Ready:
        DSPS A 1 A_WeaponReady
        loop

      Select:
        DSPS A 1 A_Raise
        loop

      Deselect:
        DSPS A 1 A_Lower
        loop

      Fire:
        DSPS A 6 A_JumpIfNoAmmo("Melee")
        DSPS A 4 A_PlayWeaponSound("dsparil/attack")
        TNT1 A 0 A_GunFlash
        DSPS B 2 bright Offset(0,33)
        DSPS B 4 bright Offset(0,37)
		TNT1 A 0 A_TakeInventory("VillainAmmoLight",4)
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
		TNT1 A 0 A_FireCustomMissile("DSparilLightning",0,1)
        DSPS C 3 bright Offset(0,58)
        DSPS C 3 bright Offset(0,52)
        DSPS B 3 bright Offset(0,46)
        DSPS B 3 bright offset(0,42)
		DSPS A 3 offset(0,39)
		DSPS A 3 offset(0,36)
		DSPS A 3 offset(0,32)
        DSPS A 4 A_ReFire
        goto ready
		
	  Melee:
        DSPS A 2 Offset(0,33)
        DSPS A 4 Offset(0,37) 
	    TNT1 A 0 A_CustomPunch(random(60,120),1,0,"StaffPuff4")
		DSPS A 6 offset(0,12)
		DSPS A 4 offset(0,18)
		DSPS A 4 offset(0,24)
		DSPS A 3 offset(0,28)
		DSPS A 2 offset(0,32)
		goto Ready
		
	  CoopFire:
		TNT1 A 0 A_FireCustomMissile("DSparilLightningCoop",0,1)
        DSPS C 3 bright Offset(0,58)
        DSPS C 3 bright Offset(0,52)
        DSPS B 3 bright Offset(0,46)
        DSPS B 3 bright offset(0,42)
		DSPS A 3 offset(0,39)
		DSPS A 3 offset(0,36)
		DSPS A 3 offset(0,32)
        DSPS A 4 A_ReFire
        goto ready

      AltFire:
		TNT1 A 0 A_JumpIfNoAmmo("Ready")
        DSPS A 10 A_JumpIfInventory("DSparilSummonCooldown",1,"Ready")
        DSPS A 5 A_PlayWeaponSound("dsparil/attack")
        TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"AltFireTLMS")
		TNT1 A 0 A_JumpIfInventory("TLMSModeOn",1,"AltFireTLMS")
        DSPS D 5 bright
        //TNT1 A 0 A_FireCustomMissile("DSparilSummon",25,0,0,0,0,0)
        //TNT1 A 0 A_FireCustomMissile("DSparilSummon",-25,0,0,0,0,0)
		TNT1 A 0 A_TakeInventory("VillainAmmoHeavy",9)
		TNT1 A 0 A_FireCustomMissile("BFGAmmoWaster")
        TNT1 A 0 A_SpawnItemEx("DSparilWizard",0,0,2,0,12,0,105,48)
        TNT1 A 0 A_SpawnItemEx("DSparilWizard2",0,0,2,0,12,0,75,48)
        DSPS E 6 bright
        DSPS D 6 bright A_GiveInventory("DSparilSummonCooldown",10)
        DSPS A 8 bright ACS_ExecuteAlways(484, 0, 1, 35)
        goto ready
		
	  AltFireTLMS:
        DSPS D 5 bright
		TNT1 A 0 A_TakeInventory("VillainAmmoHeavy",9)
		TNT1 A 0 A_FireCustomMissile("BFGAmmoWaster")
        TNT1 A 0 A_SpawnItemEx("DSparilWizardTLMS",0,0,2,0,12,0,105,48)
        TNT1 A 0 A_SpawnItemEx("DSparilWizardTLMS2",0,0,2,0,12,0,75,48)
        DSPS E 6 bright
        DSPS D 6 bright A_GiveInventory("DSparilSummonCooldown",10)
        DSPS A 8 bright ACS_ExecuteAlways(484, 0, 1, 35)
        goto ready
    }
}

actor DSparilLightning : Sorcerer2FX1
{
	+FORCERADIUSDMG
	DONTHURTSHOOTER
    Obituary "%k \croverwhelmed\c- %o \crwith despair.\c-"
    Speed 35
	States
	{
    Death:
      FX16 G 5 Bright A_Explode(random[S2FX1](80,111),128,0)	
      FX16 HIJKL 5 Bright
      Stop
	}
}

actor DSparilLightningCoop : Sorcerer2FX1
{
	+THRUSPECIES
	+FORCERADIUSDMG
	DONTHURTSHOOTER
	Species "Villain"
	DamageType "VillainTeam"
    //Obituary "%k \croverwhelmed\c- %o \crwith despair.\c-"
    Speed 35
	States
	{
    Death:
      FX16 G 5 Bright A_Explode(random[S2FX1](80,111),128,0)
      FX16 HIJKL 5 Bright
      Stop
	}
}

actor DSparilWizard : Wizard
{
    Health 90
    DamageFactor "DSparilDamage", 0.0
    //+FRIENDLY
    +THRUSPECIES
	//-SOLID
    Species "Villain"
    -SHOOTABLE
    DropItem "ArtiTomeOfPower", 16
    Obituary "\crA Disciple overwhelmed\c- %o \crwith Despair.\c-"
    States
    {
      Spawn:
        TNT1 A 0 ThrustThing(angle*256/360+96, 2, 0, 0)
        WZFX ABABABAB 5 BRIGHT A_SetTranslucent(0.9,1)//A_Wander 
        TNT1 A 0 A_Stop
        TNT1 A 0 A_SetTranslucent(1,0)
        TNT1 A 0 A_SpawnItemEx("HereticTeleport",0,0,32,0,0,0,0,128)
        WZRD A 5 A_GiveInventory("IsNarakaClass",1)
        WZRD A 5 A_SetShootable

      Spawn2:
        TNT1 A 0 A_GiveInventory("IsNarakaClass",1)
        WZRD B 10 A_Look
        WZRD A 10 A_Look
        WZRD B 10 A_Look
        goto See
		
	  Missile:
		TNT1 A 0 A_JumpIfInTargetInventory("DsparilRevived",1,"WhoopsThatsDSparil")
	    WZRD C 4 A_WizAtk1
        WZRD C 4 A_WizAtk2
        WZRD C 4 A_WizAtk1
        WZRD C 4 A_WizAtk2
        WZRD C 4 A_WizAtk1
        WZRD C 4 A_WizAtk2
        WZRD C 4 A_WizAtk1
        WZRD C 4 A_WizAtk2
		WZRD D 0 A_GhostOff
		WZRD D 0 A_CustomMissile("DSparilWizardShot",32,0,5.625,0,0)
		WZRD D 0 A_CustomMissile("DSparilWizardShot",32,0,-5.625,0,0)
		WZRD D 0 A_CustomMissile("DSparilWizardShot",32,0,0,0,0)
        WZRD D 12 A_PlaySound("wizard/attack")
        Goto See
		
	  WhoopsThatsDSparil:
		TNT1 A 0 A_ClearTarget
        WZRD A 3 A_Wander
        WZRD A 4 A_Wander
        WZRD A 3 A_Wander
        WZRD A 4 A_Wander
        WZRD B 3 A_Wander
        WZRD B 4 A_Wander
        WZRD B 3 A_Wander
        WZRD B 4 A_Wander
		goto See
    }
}

actor DSparilWizardTLMS : DSparilWizard
{
	DamageFactor "VillainTeam", 0.0
	States
	{
	  Missile:
		TNT1 A 0 A_JumpIfInTargetInventory("IsNarakaClass",1,"WhoopsThatsDSparil")
		// Well, no, it's not D'Sparil, but may as well use an existing state.
	    WZRD C 4 A_WizAtk1
        WZRD C 4 A_WizAtk2
        WZRD C 4 A_WizAtk1
        WZRD C 4 A_WizAtk2
        WZRD C 4 A_WizAtk1
        WZRD C 4 A_WizAtk2
        WZRD C 4 A_WizAtk1
        WZRD C 4 A_WizAtk2
		WZRD D 0 A_GhostOff
		WZRD D 0 A_CustomMissile("DSparilWizardShotTLMS",32,0,5.625,0,0)
		WZRD D 0 A_CustomMissile("DSparilWizardShotTLMS",32,0,-5.625,0,0)
		WZRD D 0 A_CustomMissile("DSparilWizardShotTLMS",32,0,0,0,0)
        WZRD D 12 A_PlaySound("wizard/attack")
        Goto See
	}
}

actor DSparilWizard2 : DSparilWizard
{
    States
    {
      Spawn:
        TNT1 A 0 ThrustThing(angle*256/360+17, 2, 0, 0)
        WZFX ABABABAB 5 BRIGHT A_SetTranslucent(0.9,1)//A_Wander 
        TNT1 A 0 A_Stop
        TNT1 A 0 A_SetTranslucent(1,0)
        TNT1 A 0 A_SpawnItemEx("HereticTeleport",0,0,32,0,0,0,0,128)
        WZRD A 5 A_GiveInventory("IsNarakaClass",1)
        WZRD A 5 A_SetShootable

      Spawn2:
        TNT1 A 0 A_GiveInventory("IsNarakaClass",1)
        WZRD B 10 A_Look
        WZRD A 10 A_Look
        WZRD B 10 A_Look
        goto See
    }
}

actor DSparilWizardTLMS2 : DSparilWizard2
{
	DamageFactor "VillainTeam", 0.0
	States
	{
	  Missile:
		TNT1 A 0 A_JumpIfInTargetInventory("IsNarakaClass",1,"WhoopsThatsDSparil")
	    WZRD C 4 A_WizAtk1
        WZRD C 4 A_WizAtk2
        WZRD C 4 A_WizAtk1
        WZRD C 4 A_WizAtk2
        WZRD C 4 A_WizAtk1
        WZRD C 4 A_WizAtk2
        WZRD C 4 A_WizAtk1
        WZRD C 4 A_WizAtk2
		WZRD D 0 A_GhostOff
		WZRD D 0 A_CustomMissile("DSparilWizardShotTLMS",32,0,5.625,0,0)
		WZRD D 0 A_CustomMissile("DSparilWizardShotTLMS",32,0,-5.625,0,0)
		WZRD D 0 A_CustomMissile("DSparilWizardShotTLMS",32,0,0,0,0)
        WZRD D 12 A_PlaySound("wizard/attack")
        Goto See
	}
}

actor HereticTeleport
{
    +NOBLOCKMAP
    +NOTELEPORT
    +NOGRAVITY
    +NOINTERACTION
    +CLIENTSIDEONLY
    Alpha 0.9
    RenderStyle Add
    States
    {
      Spawn:
        HTEL ABCDEFGHGFEDC 6 Bright
        Stop
    }
}

actor DSparilWizardShot : WizardFX1
{
    Obituary "\crA Disciple overwhelmed\c- %o \crwith Despair.\c-"
    DamageType "DSparilDamage"
}

actor DSparilWizardShotTLMS : WizardFX1
{
    Obituary "\crA Disciple overwhelmed\c- %o \crwith Despair.\c-"
    DamageType "VillainTeam"
}

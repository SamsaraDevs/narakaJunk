Actor CyberdemonClass : Boolean { }

actor EmptyTelefog : TeleportFog
{
    Alpha 0
    States
    {
      Spawn:
        ACLO E 1
        stop
    }
}

actor CyberdemonPlayer : PlayerPawn
{
    LimitedToTeam 1
    Player.DisplayName "Cyberdemon"
    BloodColor "Red"
    Radius 20 // Previously 25
    Height 67
    Mass 1000
    Player.Viewheight 67
    Player.AttackZOffset 22
    Player.ForwardMove 0.65 0.65
    Player.SideMove 0.55 0.55
    Player.JumpZ 6//Default is 8
    Player.StartItem "Cyber-Rocket Launcher", 1
    Player.StartItem "CyberdemonClass", 1
	Player.StartItem "IsSamsaraClass", 1
    //Player.StartItem "BossDamageImmunity", 1
    Player.Soundclass "Cyberdemon"
    DamageFactor "DSparilDamage", 0
    DamageFactor "Slime", 0
    DamageFactor "Lava", 0
    DamageFactor "Normal", 0.45
    DamageFactor "Telefrag", 2.0
    DamageFactor "PoisonCloud", 1.75
    DamageFactor "DukeExplosive", 0.25
    DamageFactor "TimonsAxe", 0.25
    DamageFactor "WMCBeam", 0.25
    DamageFactor "WMCNade", 0.25
    Species "Villain"
    //DamageFactor "MarathonFusion", 2.0 // Maybe not for this.
    //DamageFactor "CorvusHellstaffTomed", 0.2 // Or this...
    +THRUSPECIES
    +NOSKIN
    -DONTBLAST
    +DONTRIP
    +BOSSDEATH
    +NOTDMATCH
    +NOICEDEATH
    +NORADIUSDMG
    //Species "Player"
    Player.ColorRange 112, 127
    States
    {
      Spawn:
        CYBD A -1
        Loop
        
      See:
        CYBD A 3
        CYBD A 0 A_CheckFloor("WalkSound")
        CYBD A 3
        goto ContinueOn

      WalkSound:
        TNT1 A 0 Radius_Quake(2,4,0,2,0)
        CYBD A 3 A_PlaySoundEx("cyber/hoof","soundslot6")
        TNT1 A 0 A_SpawnItemEx("CyberdemonStomp",0,0,0,0,0,0,0,48)//A_Explode(128,192,0)

      ContinueOn:
        CYBD ABB 6
        CYBD C 3
        CYBD A 0 A_CheckFloor("WalkSound2")
        CYBD C 3
        goto ContinueOn2

      WalkSound2:
        TNT1 A 0 Radius_Quake(2,4,0,2,0)
        CYBD C 3 A_PlaySoundEx("cyber/hoof","soundslot6")
        TNT1 A 0 A_SpawnItemEx("CyberdemonStomp",0,0,0,0,0,0,0,48)//A_Explode(128,192,0)

      ContinueOn2:
        CYBD CDD 6 
        goto Spawn
        
      Missile:
        CYBD E 12 
        Goto Spawn
        
      Melee:
        CYBD F 6 BRIGHT
        Goto Missile
        
      Pain:
        CYBD G 4 
        CYBD G 4 A_Pain
        Goto Spawn
        
      Death.DukeDamage:
      Death.DukeExplosive:
      Death.DukePistol:
      Death.MightyBoot:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBossKillTaunt",1)
        goto Death
        
      XDeath:
      Death:
        CYBD H 1 A_SpawnItemEx("KilledCyberdemon",0,0,0,0,0,0,0,33)
        TNT1 A 0 A_PlayerScream
        TNT1 A 0 A_NoBlocking
        ACLO E -1
        stop
        
      Death.DukeIce:
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeBossKillTaunt",1)
        goto Ice
        
      Death.ZorchPistol:
      Death.Zorch:
        CYBD G 0 A_Stop
        CYBD G 1 A_SpawnItemEx("ZorchedCyberdemon",0,0,0,0,0,0,0,33)
        TNT1 A 0 A_NoBlocking
        ACLO E -1
        stop
        
      Ice:
      Death.HexenIce:
      Death.Ice:
        CYBD G 1 A_SpawnItemEx("FrozenCyberdemon",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_NoBlocking
        ACLO E -1
        stop
    }
}

ACTOR CyberdemonStomp
{
    Radius 1
    Height 1
	Speed 0
    +NOCLIP
    -SOLID
    Obituary "%k stomped %o under cloven hooves."
    States
    {
      Spawn:
		TNT1 A 0
		TNT1 A 1 A_Explode(128,192,0)
        Stop
    }
}

ACTOR KilledCyberdemon
{
    Radius 1
    Height 1
    +NOCLIP
    -SOLID
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        CYBD H 8
        CYBD I 8 //A_PlayerScream
        CYBD JKL 8
        CYBD M 8
        CYBD NO 8
        CYBD P 10
        CYBD P -1
        Stop
    }
}

ACTOR ZorchedCyberdemon
{
    Radius 1
    Height 1
    +NOCLIP
    -SOLID
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_PlaySoundEx("chex/zorchhuge","soundslot5",0,0)
        CYBD G 8 A_SpawnItem("ZorchEffectHuge")
        CYBD G 8 A_SetTranslucent(0.9,0)
        CYBD G 0 A_PlaySoundEx("chex/cyberzorch","soundslot6",0,2)
        CYBD G 8 A_SetTranslucent(0.8,0)
        CYBD G 0 A_NoBlocking
        CYBD G 8 A_SetTranslucent(0.7,0)
        CYBD G 8 A_SetTranslucent(0.6,0)
        CYBD G 8 A_SetTranslucent(0.5,0)
        CYBD G 8 A_SetTranslucent(0.4,0)
        CYBD G 8 A_SetTranslucent(0.3,0)
        CYBD G 8 A_SetTranslucent(0.2,0)
        CYBD G 8 A_SetTranslucent(0.1,0)
        TNT1 A 1 A_SetTranslucent(1,0)
        TNT1 A -1
        stop
    }
}

ACTOR FrozenCyberdemon
{
    Radius 1
    Height 1
    +NOCLIP
    -SOLID
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        CYBD G 0 A_PlaySoundEx("freeze/cyberdemon","soundslot6",0,0)
        CYBD G 0 //A_GenericFreezeDeath
        CYBD G 0 A_UnsetShootable
        CYBD Z 105 A_UnsetSolid
        CYBD G 0 A_PlaySoundEx("freeze/break","soundslot6",0,0)
        CYBD GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 0 A_SpawnItemEx("IceChunk2",random(-1,1),random(-1,1),random(16,96),random(-2,2),random(-2,2),random(5,15))//A_SpawnDebris("IceChunk2")
        CYBD G 0 A_IceGuyDie
        ACLO E 1 A_IceGuyDie
        ACLO E -1
        stop
    }
}

ACTOR "Cyber-Rocket Launcher" : Weapon
{
    //Weapon.AmmoType1 "PlasmaGunAmmo"
    //Weapon.AmmoUse1 1
    //Weapon.AmmoType2 "CannonAmmo"
    //Weapon.AmmoUse2 1
    //+NOAUTOAIM
    Weapon.SlotNumber 9
    +AMMO_OPTIONAL
    +ALT_AMMO_OPTIONAL
    Weapon.Kickback 100
    States
    {
      Ready:
        CYBL A 1 A_WeaponReady
        Loop

      Deselect:
        CYBL A 1 A_Lower
        Loop

      Select:
        CYBL A 1 A_Raise
        Loop

      Fire:
        CYBL A 1 ACS_ExecuteAlways(474)
	  Hold:
		CYBL A 3
        CYBL B 4
        CYBL A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopHold")
        CYBL C 4 A_FireCustomMissile("CyberRocket2",0,0,-10,0)
        CYBL DE 4
		CYBL A 1 A_Refire
		CYBL A 1 ACS_ExecuteAlways(476)
		CYBL A 1 A_ClearRefire
        goto Ready
	 
	 CoopHold:
        CYBL C 4 A_FireCustomMissile("CyberRocketCoop",0,0,-10,0)
        CYBL DE 4
		CYBL A 1 A_Refire
		CYBL A 1 ACS_ExecuteAlways(476)
		CYBL A 1 A_ClearRefire
        goto Ready

      AltFire:
        CYBL A 1 ACS_ExecuteAlways(475)
	  AltHold:
		CYBL A 1
        CYBL A 0 A_GunFlash
        CYBL B 3
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopAltHold")
        CYBL C 3 A_FireCustomMissile("CyberRocket3",random(-1,4),0,-10,0,random(-2,2))
        CYBL D 4
        CYBL E 1
        CYBL A 0 A_GunFlash
        CYBL B 3
        CYBL C 3 A_FireCustomMissile("CyberRocket3",random(-1,4),0,-10,0,random(-2,2))
        CYBL D 4
        CYBL E 1
        CYBL A 0 A_GunFlash
        CYBL B 3
        CYBL C 3 A_FireCustomMissile("CyberRocket3",random(-1,4),0,-10,0,random(-2,2))
        CYBL D 4
        CYBL E 5
        CYBL A 10
		CYBL A 1 A_Refire
		CYBL A 1 ACS_ExecuteAlways(476)
		CYBL A 1 A_ClearRefire
        goto Ready
		
	  CoopAltHold:
        CYBL C 3 A_FireCustomMissile("CyberRocketCoop2",random(-1,4),0,-10,0,random(-2,2))
        CYBL D 4
        CYBL E 1
        CYBL A 0 A_GunFlash
        CYBL B 3
        CYBL C 3 A_FireCustomMissile("CyberRocketCoop2",random(-1,4),0,-10,0,random(-2,2))
        CYBL D 4
        CYBL E 1
        CYBL A 0 A_GunFlash
        CYBL B 3
        CYBL C 3 A_FireCustomMissile("CyberRocketCoop2",random(-1,4),0,-10,0,random(-2,2))
        CYBL D 4
        CYBL E 5
        CYBL A 10
		CYBL A 1 A_Refire
		CYBL A 1 ACS_ExecuteAlways(476)
		CYBL A 1 A_ClearRefire
        goto Ready
    }
}

ACTOR CyberRocket2 : CyberRocket
{
    Decal Scorch
    SeeSound "doomguy/rocklf"
    DeathSound "doomguy/rocklx"
    Obituary "%k splattered %o with a Cyber-Rocket."
    Speed 30
    States
    {
      Spawn:
        DMSL A 1 bright
        Loop

      Death:
        DMSL B 8 bright A_Explode
        DMSL C 6 bright
        DMSL D 4 bright
        Stop
    }
}

ACTOR CyberRocketCoop : CyberRocket2
{
	+THRUSPECIES
	Species "Villain"
}

ACTOR CyberRocket3 : CyberRocket2
{
    Obituary "%k splattered %o with a Cyber-Rocket."
    Speed 20
}

ACTOR CyberRocketCoop2 : CyberRocket3
{
	+THRUSPECIES
	Species "Villain"
}

ACTOR CyberSpeed : PowerSpeed
{
  Powerup.Duration -45
  Speed 0.75
}

ACTOR CyberSpeed2 : PowerSpeed
{
  Powerup.Duration -45
  Speed 0.45
}